PREFIX schema: <https://schema.org/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>

CONSTRUCT {
    # repeat the old, for fun
    ?s a schema:Dataset .
    ?s schema:spatialCoverage ?spatialcoverage .
    ?spatialcoverage schema:geo ?geo .
    ?geo a schema:GeoCoordinates .
    ?geo schema:latitude ?lat .
    ?geo schema:longitude ?long .

    ?geo geo:hasGeometry ?geometry .
    ?geometry a geo:Geometry .
    # or a <http://www.opengis.net/ont/sf#Point>    might be better
    ?geometry geo:asWKT ?wkt .
}
WHERE {
    ?s a schema:Dataset .
    ?s schema:spatialCoverage ?spatialcoverage .
    ?spatialcoverage schema:geo ?geo .
    ?geo a schema:GeoCoordinates .
    ?geo schema:latitude ?lat .
    ?geo schema:longitude ?long .

    BIND(IRI(CONCAT(STR(?geo), "/geometry")) AS ?geometry)
    BIND(STRDT(  CONCAT("POINT(", STR(?long), " ", STR(?lat), ")"),  geo:wktLiteral  ) AS ?wkt)
}
