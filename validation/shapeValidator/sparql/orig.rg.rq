PREFIX shacl: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>

SELECT ?id ?name ?severity ?grname (STR(?refurl) AS ?str_refurl) (STR(?refresource) AS ?str_refresrouce) ?constraint ?path ?message (STR(?focus) AS ?focusURL) ?focusType ?value
WHERE {
  ?id rdf:type shacl:ValidationResult .
  ?id shacl:focusNode ?focus .
  ?id shacl:sourceShape ?ss .
  ?focus rdf:type ?focusType .
  ?id shacl:resultMessage ?message .
  ?id shacl:resultSeverity ?severity .
  ?id shacl:sourceConstraintComponent ?constraint .
  OPTIONAL {  ?ss shacl:name ?name . }
  OPTIONAL {
    ?ss shacl:group ?group .
    ?group rdfs:label ?grname .
    ?group schema:url ?refurl .
    ?group rdfs:isDefinedBy ?refresource .
   }
  OPTIONAL { ?id shacl:resultPath ?path . }
  OPTIONAL { ?id shacl:value ?value . }
}


